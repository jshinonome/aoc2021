p:{x:" "\:x;(b:"on"~x 0;"J"$".."\:'2_'","\:x 1)}

r:p'r:0:`:22.txt

/ filter
f:{~(|/x[1;;1]<-50)||/x[1;;0]>50}

r0:r@&f'r

c:101#,101#,101#0b

s:{x[0]+!1+x[1]-x[0]}

/ Q1
/ 2121 ms 14046304
(+//){
    {[c;b;x;y;z]
        (c;b;x;y;z);
        .[;;:;b]/[c;(,/)(50+s x),/:\:(,/)(50+s y),/:\:(50+s z)]
    }[x;y 0;y[1;0];y[1;1];y[1;2]]
 }/[c;r0]

/ Q1 alternative
/ 7 ms 47312
+/{*/1+{y-x}.+x}'{
    ((,/)@o[;y 1]'x),$[y 0;,y 1;()]
 }/[,r0[0;1];1_r0]


/ Q2
/ intersect
it:{~|/|/((1 -1)*|+x)<(+y)*(1 -1)}

/ is valid cuboid
iv:{~|/(>).+x}

o:{
    if[~|/it[x;y];:,x];
    z01:y[0;0]|x[0;0];
    z02:y[0;1]&x[0;1];
    z11:y[1;0]|x[1;0];
    z21:y[1;1]&x[1;1];
    cd:(
        ((x[0;0];x[0;1]&y[0;0]-1);x 1;x 2);
        ((x[0;0]|y[0;1]+1;x[0;1]);x 1;x 2);
        ((z01;z02);(x[1;0];x[1;1]&y[1;0]-1);x 2);
        ((z01;z02);(x[1;0]|y[1;1]+1;x[1;1]);x 2);
        ((z01;z02);(z11;z21);(x[2;0];x[2;1]&y[2;0]-1));
        ((z01;z02);(z11;z21);(x[2;0]|y[2;1]+1;x[2;1]))
    );
    :cd@&iv'cd
 }

/ 907 ms 749712
a:{
    ((,/)@o[;y 1]'x),$[y 0;,y 1;()]
 }/[,r[0;1];1_r]

+/{*/1+{y-x}.+x}'a
